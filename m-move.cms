//#name "m-move"
// Author: VINT
// Version: 1.4 (28.02.2013)
// Скрипт для Clickermann v4.9 005
// подключение m-move:
// #include "..\Libs\m-move.cms"


SUB(m_move_old, $MOVE_END_X, $MOVE_END_Y)
    // Пример вызова:
    // m_move_old(150, 150)
    
    $MOVE_SPEED = 16		// множитель скорости в попугаях меньше-быстрее. Можно дробный
    $MOVE_Start_X = $_xmouse
    $MOVE_Start_Y = $_ymouse
    $MOVE_X = $MOVE_Start_X
    $MOVE_Y = $MOVE_Start_Y
    
    $dist = DIST($MOVE_END_X, $MOVE_END_Y, $MOVE_Start_X, $MOVE_Start_Y)
    
    $MOVE_SPEEDVAR = INT((SQRT($dist)) / $MOVE_SPEED)
    //LOGWRITE("dist  ", $dist)
    //LOGWRITE("MOVE_SPEEDVAR  ", $MOVE_SPEEDVAR)
    
    IF($MOVE_SPEEDVAR < 1)
        $MOVE_SPEEDVAR = 1
    END_IF
    
    $MOVE_step = ($dist-1) / $MOVE_SPEEDVAR
    $MOVE_X_step = ($MOVE_END_X-$MOVE_Start_X) / $MOVE_step
    $MOVE_Y_step = ($MOVE_END_Y-$MOVE_Start_Y) / $MOVE_step
    
    FOR($MOVE_i=0, $MOVE_i < INT($MOVE_step))
        $MOVE_X = $MOVE_X + $MOVE_X_step
        $MOVE_Y = $MOVE_Y + $MOVE_Y_step
        MOVE(ROUND($MOVE_X,0), ROUND($MOVE_Y,0))
        WAITMS(1)
    END_CYC
    
    MOVE($MOVE_END_X, $MOVE_END_Y)		//Передвинуть окончательно в заданную точку
END_SUB

SUB(mouse_move, $MOVE_END_X, $MOVE_END_Y)
    $MOVE_Start_X = $_xmouse
    $MOVE_Start_Y = $_ymouse
    $MOVE_X = $MOVE_Start_X
    $MOVE_Y = $MOVE_Start_Y
    
    $dist = DIST($MOVE_END_X, $MOVE_END_Y, $MOVE_Start_X, $MOVE_Start_Y)
    $MOVE_step = ($dist-1) / INT(SQRT($dist))
    //LOGWRITE("$MOVE_step  ", $MOVE_step)
    $MOVE_X_step = ($MOVE_END_X-$MOVE_Start_X) / $MOVE_step
    $MOVE_Y_step = ($MOVE_END_Y-$MOVE_Start_Y) / $MOVE_step
    
    FOR($MOVE_i=0, $MOVE_i < INT($MOVE_step))
        $MOVE_X = $MOVE_X + $MOVE_X_step
        $MOVE_Y = $MOVE_Y + $MOVE_Y_step
        MOVE(INT($MOVE_X) + RND(-2,2), INT($MOVE_Y) + RND(-2,2))
        WAITMS($rnwait) // задержка при движении по точкам
    END_CYC
END_SUB

SUB(m_move, $M_X, $M_Y)
    $rnwait = RND(6,15) // случайная скорость
    $MOVE_MID_X = INT(($_xmouse+$M_X)/2)
    $MOVE_MID_Y = INT(($_ymouse+$M_Y)/2)
    $otx = INT(ABS($_xmouse - $M_X) * 0.12)
    $oty = INT(ABS($_ymouse - $M_Y) * 0.12)
    mouse_move($MOVE_MID_X+RND(-$otx,$otx), $MOVE_MID_Y+RND(-$oty,$oty))
    mouse_move($M_X,$M_Y)
    MOVE($M_X,$M_Y)		//Передвинуть окончательно в заданную точку
END_SUB

//m_move(150, 150)
//HALT

SUB(m_lclick, $MOVE_END_X, $MOVE_END_Y)
    // Пример вызова:
    // m_lclick(150, 150)
    $LC1 = $MOVE_END_X
    $LC2 = $MOVE_END_Y
    m_move($MOVE_END_X, $MOVE_END_Y)
    WAITMS(20)
    LDOWN($LC1, $LC2)
    WAITMS(30)
    LUP($LC1, $LC2)
    WAITMS(30)
END_SUB

SUB(m_mclick, $MOVE_END_X, $MOVE_END_Y)
    $LC1 = $MOVE_END_X
    $LC2 = $MOVE_END_Y
    m_move($MOVE_END_X, $MOVE_END_Y)
    WAITMS(RND(200,300))
    MDOWN($LC1, $LC2)
    WAITMS(30)
    MUP($LC1, $LC2)
    WAITMS(30)
END_SUB

