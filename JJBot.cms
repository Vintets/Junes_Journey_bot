#name "Junes Journey bot"
// Author: Vint
// Version: 1.4 (15.07.2024)
// Скрипт для Clickermann v4.14.003b

#define @CRLF: STRCONCAT(char(13),char(10))
#define \n: STRCONCAT(char(13),char(10))
#define @TAB: char(9)

#include "arrey.cms"
#include "string.cms"


//                              Настройки
//++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

//Настройки NoxPlayer
// разрешение окна NoxPlayer            1604x934
// разрешение экрана рабочей области    1600x900
// ориентация  - альбомная

// номер сцены по умолчанию
$default_scene_number = 1

// уровень логирования 0-NOTSET, 1-CRITICAL 2-ERROR 3-WARNING 4-INFO 5-DEBUG
$loglevel = 5

// главная задержка после клика (при автомате)
$wait_click_found = 500 //250

// тест 1 - без ожидания стадий, 2 - по скрину
$test = 1

$scenes_path = "scenes\"


//-------------------------------------------
$ocrexe = "Capture2Text\Capture2Text_CLI.exe"  // lnk
$ocrimg[0] = STRCONCAT($scenes_path, "ocr_img1.bmp")
$ocrimg[1] = STRCONCAT($scenes_path, "ocr_img2.bmp")
$ocrimg[2] = STRCONCAT($scenes_path, "ocr_img3.bmp")
$ocrimgdummy = "textures\ocr_imgdummy.bmp"
$ocroutfile = STRCONCAT($scenes_path, "ocr_result.txt")
$scenes_ini = STRCONCAT($scenes_path, "scenes.ini")
$screenshots_scenes_path = STRCONCAT($scenes_path, "screenshots_scenes\")
$allowed_char = " абвгдеёжзийклмнопрстуфхцчшщъыьэюяАБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ0123456789«»"

// out file
$ocrparbase = STRCONCAT(" --language Russian -o ", $ocroutfile)

//++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
//≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡

SUB(nox_window)
    // получение параметров NoxPlayer
    $nox = WNDFIND("NoxPlayer")
    WNDGETINFO($nox, $win_x, $win_y, $win_w, $win_h)
    IF($loglevel >= 4)
        LOGWRITE("Nox: ", $nox, ", ", $win_x, ",", $win_y, "  ", $win_w, "x", $win_h)
        // 227, 87      1604x934
        // 229, 119     1600x900
    END_IF
    
    IF($nox == 0)
        LOGWRITEC("Не найдено окно Nox", 255)
        HALT
    END_IF
    IF(($win_x < 0) | ($win_y < 0))
        LOGWRITEC("Окно Nox свёрнуто", 255)
        HALT
    END_IF
END_SUB

SUB(init)
    LOGCLEAR
    IF($loglevel > 0)
        LOGSHOW(1, 1920, 510)
        WAITMS(300)
        $hwnd_log = WNDFIND("Лог")
        WNDSIZE($hwnd_log, 366, 500)
        LOGSHOW(1, 1920, 510)
    ELSE
        WAITMS(200)
    END_IF
    
    $imdelta_x = 0
    $imdelta_y = 0
    
    // window NoxPlayer
    IF($test < 2)
        nox_window()
    ELSE
        $win_x = 160 - 2
        $win_y = 110 - 32
        $win_w = 1604
        $win_h = 934
    END_IF
    
    // window sub
    $wx1 = $win_x + 2
    $wy1 = $win_y + 32
    $win_w = $win_w - 4
    $win_h = $win_h - 34
    $wx2 = $wx1 + $win_w - 1
    $wy2 = $wy1 + $win_h - 1
    IF($loglevel >= 4)
        LOGWRITE("Main field: ", $wx1, ",", $wy1, "  ", $wx2, ",", $wy2, "  ", $win_w, "x", $win_h)
    END_IF
    
    // items zone
    $itx1 = $wx1 + 36
    $ity1 = $wy1 + 821
    $itx2 = $wx1 + 1407
    $ity2 = $wy1 + 864
    
    // play button delta
    $play_x = 1495
    $play_y = 795
    
    $location = ""
    
    LOGWRITE(\n, \n, "*************************************************")
END_SUB

SUB(find_pic_nw, $fpnw_x1,$fpnw_y1,$fpnw_x2,$fpnw_y2, $fpnw_image, $fpnw_percent, $fpnw_colorm)
    // поиск картинки без ожидания
    $is_found = #FALSE
    $fpnw_x1 = $wx1 + $fpnw_x1
    $fpnw_y1 = $wy1 + $fpnw_y1
    $fpnw_x2 = $wx1 + $fpnw_x2
    $fpnw_y2 = $wy1 + $fpnw_y2
    GETSCREEN($fpnw_x1,$fpnw_y1,$fpnw_x2,$fpnw_y2)
    IF($fpnw_colorm != 0)
        COLORMODE($fpnw_colorm, $fpnw_x1,$fpnw_y1,$fpnw_x2,$fpnw_y2)
    END_IF
    IF_PICTURE_IN($fpnw_x1,$fpnw_y1,$fpnw_x2,$fpnw_y2, STRCONCAT("textures\", $fpnw_image), 65280, $fpnw_percent)
        $is_found = #TRUE
        $found_x = $_return1
        $found_y = $_return2
        IF($loglevel >= 4)
            LOGWRITE("Найдено  ", $fpnw_image, "  в ", $_return1, "/", $_return2)
        END_IF
    ELSE
        WAITMS(20)
    END_IF
END_SUB

SUB(find_pic_wait, $fpw_x1,$fpw_y1,$fpw_x2,$fpw_y2,$fpw_image, $fpw_percent, $fpw_colorm)
    // ожидание картинки
    $is_found = #FALSE
    $time_notfound = $_ms + 20000		// 20 сек
    WHILE(($is_found == #FALSE) & ($time_notfound > $_ms))
        find_pic_nw($fpw_x1,$fpw_y1,$fpw_x2,$fpw_y2, $fpw_image, $fpw_percent, $fpw_colorm)
    END_CYC
    IF($is_found == #FALSE)
        LOGWRITE("время истекло, картинка не найдена")
    END_IF
END_SUB

SUB(get_location)
    // locations:
    // load game - экран загрузки игры
    // *main - главный экран (остров орхидей)

    // *hidden object scene - сцена поиска предметов
    // detective - требуются детективы
    // cafe - кафе
    // drink - выставить напиток
    // *touch for start - оснитесь, чтобы начать
    // help Wanted - требуется помощь
    // advertising - реклама
    // advertising is over - реклама закончилась

    $location = ""

    IF($location = "")
        find_pic_nw(1518,32,1562,73, "pause.bmp", 100, 7)
        IF($is_found == #TRUE)
            $location = "hidden object scene"
        END_IF
    END_IF

    IF($location = "")
        find_pic_nw(0,0,$win_w-1,$win_h-1, "money.bmp", 80, 0)
        IF($is_found == #TRUE)
            $location = "main"
        END_IF
    END_IF

    IF($location = "")
        find_pic_nw(270, 420, 800, 470, "touch.bmp", 95, 0)
        IF($is_found == #TRUE)
            $location = "touch for start"
        END_IF
    END_IF
END_SUB

SUB(get_hash_scene)
    $isDataScene = #FALSE
    
    GETSCREEN($wx1,$wy2-4, $wx1+999,$wy2)
    $hash_scene = PXLCRC($wx1,$wy2-4, $wx1+999,$wy2)
    LOGWRITE("hash_scene ", $hash_scene)
    
    $hash_scene_file = INIREAD($scenes_ini, "hash_scene", $hash_scene)
    IF($hash_scene_file != "")
        $isDataScene = #TRUE
    END_IF
END_SUB

SUB(is_task_fill)
    // проверяем какие задания есть, а какие закончились (из 3-х)
    UNDEFINE($tasks)
    // task 1
    IF_PICTURE_IN($wx1+50,$wy1+865, $wx1+430,$wy1+882, "textures\is_task.bmp", -1, 100)
        ARRPUSH($tasks, #TRUE)
    ELSE
        ARRPUSH($tasks, #FALSE)
    END_IF
    // task 2
    IF_PICTURE_IN($wx1+530,$wy1+865, $wx1+910,$wy1+882, "textures\is_task.bmp", -1, 100)
        ARRPUSH($tasks, #TRUE)
    ELSE
        ARRPUSH($tasks, #FALSE)
    END_IF
    // task 3
    IF_PICTURE_IN($wx1+1010,$wy1+865, $wx1+1390,$wy1+882, "textures\is_task.bmp", -1, 100)
        ARRPUSH($tasks, #TRUE)
    ELSE
        ARRPUSH($tasks, #FALSE)
    END_IF
    IF($loglevel >= 4)
        LOGWRITE("Tasks  ", $tasks[0], "-", $tasks[1], "-", $tasks[2])
    END_IF
END_SUB

SUB(correct_photo_items, $cpi_nums)
    $param_main = STRCONCAT(" /C Start Python correct_img.py ", $cpi_nums)
    // LOGWRITE("$param_main ", $param_main)
    EXECUTE("cmd.exe", $param_main)
END_SUB

SUB(photo_items_all)
    GETSCREEN($itx1, $ity1, $itx2, $ity2+18)
    COLORMODE(8, $itx1, $ity1, $itx2, $ity2+18)
    //SCREENSHOTFIX($wx1,$wy1, $wx2,$wy2, "screens\items.bmp", 0)
    is_task_fill()
END_SUB

SUB(photo_items)
    // сохранение снимков слов
    photo_items_all()
    // интервал X  477,
    // Ширина поля 417    $ipw = 417
    $correct_nums = ""
    FOR($i=0, $i < 3)
        IF($tasks[$i])
            SCREENSHOTFIX($wx1 + 36 + ($i*477),$ity1, $wx1 + 453 + ($i*477),$ity2, $ocrimg[$i], 0)
            IF($correct_nums == "")
                $correct_nums = STRCONCAT($correct_nums, $i + 1)
            ELSE
                $correct_nums = STRCONCAT($correct_nums, " ", $i + 1)
            END_IF
        END_IF
    END_CYC
    // SCREENSHOTFIX($wx1+ 36,$ity1, $wx1+ 453,$ity2, $ocrimg[0], 0)
    // SCREENSHOTFIX($wx1+513,$ity1, $wx1+ 930,$ity2, $ocrimg[1], 0)
    // SCREENSHOTFIX($wx1+990,$ity1, $wx1+1407,$ity2, $ocrimg[2], 0)
    correct_photo_items($correct_nums)
END_SUB

SUB(main_screenshot_scene)
    $screenshot_scene = STRCONCAT($screenshots_scenes_path, "Scene_", $scene_number, "_", $hash_scene, ".bmp")
    GETSCREEN($wx1,$wy1, $wx2,$wy2)
    SCREENSHOTFIX($wx1,$wy1, $wx2,$wy2, $screenshot_scene, 0)
END_SUB

SUB(click, $cldelta_x, $cldelta_y)
    IF($is_found == #TRUE)
        IF($test < 2)
            LCLICK($wx1+$cldelta_x, $wy1+$cldelta_y)
        ELSE
            MOVE($wx1+$cldelta_x, $wy1+$cldelta_y)  // если тест
        END_IF
        IF($loglevel >= 5)
            LOGWRITE("Клик ", $wx1+$cldelta_x, "  ", $wy1+$cldelta_y)
        END_IF
        WAITMS(300)
    END_IF
END_SUB

SUB(click_found, $clf_delta_x, $clf_delta_y)
    IF($is_found == #TRUE)
        IF($test < 2)
            LCLICK($found_x + $clf_delta_x, $found_y + $clf_delta_y)
        ELSE
            MOVE($found_x + $clf_delta_x, $found_y + $clf_delta_y)  // если тест
        END_IF
        IF($loglevel >= 5)
            LOGWRITE("Клик ", $found_x+$clf_delta_x, "  ", $found_y+$clf_delta_y)
        END_IF
        WAITMS(300)
    END_IF
END_SUB

SUB(read_data_scene)
    UNDEFINE($items)
    
    $items_filename = STRCONCAT($scenes_path, $scene_number, "_", $hash_scene, ".txt")
    $scene_folder = STRCONCAT($scenes_path, "scene_", $scene_number)
    //LOGWRITE($items_filename)
    
    IF($isDataScene == #FALSE)
        IF($loglevel >= 3)
            LOGWRITE("Нет данных по уровню")
        END_IF
        INIWRITE($scenes_ini, "scene_number", $scene_number, $hash_scene)
        INIWRITE($scenes_ini, "hash_scene", $hash_scene, $hash_scene)
        //FCREATE($scene_folder)
    ELSE
        IF($loglevel >= 4)
            LOGWRITE("Найдены данные уровня ", $hash_scene_file)
        END_IF
        //TFREADARR($items_filename, $items)
    END_IF
END_SUB

SUB(read_items)
    UNDEFINE($items_str)
    UNDEFINE($items)
    $items_filename = STRCONCAT($scenes_path, $scene_number, "_", $hash_scene, ".txt")
    TFREADARR($items_filename, $items_str)
    $items_str_size = ARRSIZE($items_str)
    IF($loglevel >= 5)
        LOGWRITE("-INFO- size items ", $items_str_size)
    END_IF
    FOR($i=0, $i < $items_str_size)
        STRSEPARATE($items_str[$i], ",", $data)
        ARRPUSH($items, $data[0])
        ARRPUSH($items, $data[1])
        ARRPUSH($items, $data[2])
    END_CYC
END_SUB

SUB(create_ocr_par)
    $ocrpar = $ocrparbase
    FOR($i=0, $i < 3)
        IF($tasks[$i])
            $ocrpar = STRCONCAT($ocrpar, " -i ", $ocrimg[$i])
        // ELSE
        //     $ocrpar = STRCONCAT($ocrpar, " -i ", $ocrimgdummy)
        END_IF
    END_CYC
    IF($loglevel >= 5)
        LOGWRITE($ocrpar)
    END_IF
END_SUB

SUB(ocr, $ocr_par)
    EXECUTE($ocrexe, $ocr_par)
    WAITMS(100)
    
    $waitw = $_time_t + 10 //Ждать $ocrexe не больше секунд
    WHILE(WNDFIND($ocrexe) > 0 & $waitw >= $_time_t)
        WAITMS(50)
    END_CYC
END_SUB

SUB(ocr_result_filter_char)
    IF($allowed_char != "")
        FOR($res_i=0, $res_i < ARRSIZE($ocr_result))
            $ocr_result[$res_i] = STRFILTER($ocr_result[$res_i], $allowed_char, 1)
        END_CYC
    END_IF
END_SUB

SUB(ocr_result_strip)
    FOR($res_i=0, $res_i < ARRSIZE($ocr_result))
        strip($ocr_result[$res_i])
        $ocr_result[$res_i] = $strip_str
    END_CYC
END_SUB

SUB(read_ocr_result)
    UNDEFINE($ocr_result)
    TFREADARR($ocroutfile, $ocr_result)
    FOR($res_i=1, $res_i < ARRSIZE($ocr_result))
        $ocr_result[$res_i] = STRCUT($ocr_result[$res_i], 2, 40)
    END_CYC
    ocr_result_filter_char()
    ocr_result_strip()
    IF($loglevel >= 4)
        LOGWRITE("Слова: ")
        LOG_ARR("$ocr_result", ARRSIZE($ocr_result))
        // LOGWRITE("Слова: -", $ocr_result[0], "-", $ocr_result[1], "-", $ocr_result[2], "-")
    END_IF
END_SUB

SUB(find_click_3words)
    $found_words = #FALSE
    FOR($iocr=0, $iocr < ARRSIZE($ocr_result))
        $word = $ocr_result[$iocr]
        IF($word != "")
            $found_item = #FALSE
            $items_size = ARRSIZE($items)
            FOR($index=0, $index < $items_size, 3)
                $item_name = $items[$index]
                //LOGWRITE($word, "==", $item_name)
                IF($word = $item_name)
                    $item_x = $items[$index+1]
                    $item_y = $items[$index+2]
                    $index = 1000
                    $found_item = #TRUE
                    $found_words = #TRUE
                END_IF
            END_CYC
            
            IF($found_item == #TRUE)
                LOGWRITEC(STRCONCAT("Найден предмет: '", $word, "' ", $item_x, "x", $item_y), 433934)
                IF($test < 2)
                    LCLICK($wx1 + $item_x, $wy1 + $item_y)
                ELSE
                    MOVE($wx1 + $item_x, $wy1 + $item_y)  // тест
                END_IF
                WAITMS($wait_click_found)
            ELSE
                IF($loglevel >= 4)
                    LOGWRITE("нет данных для - ", $word)
                END_IF
            END_IF
        END_IF
    END_CYC
END_SUB

SUB(automat_click_words)
    // поиск извесных картинок автоматом
    photo_items()
    create_ocr_par()
    ocr($ocrpar)
    read_ocr_result()
    //read_items()
    find_click_3words()
END_SUB

SUB(touch_for_start)
    IF($test == 0)
        $is_found = #FALSE
        WHILE($is_found == #FALSE)
            find_pic_wait(270, 420, 800, 470, "touch.bmp", 95, 0)
        END_CYC
        click(800, 450)
        WAITMS(500)
    END_IF
END_SUB

SUB(add_item, $it_name, $it_x, $it_y)
    LOGWRITEC(STRCONCAT("Добавяем предмет: '", $it_name, "' ", $it_x, "x", $it_y), 16711680) // синий
    ARRPUSH($items, $it_name)
    ARRPUSH($items, $it_x)
    ARRPUSH($items, $it_y)
    $new_item = STRCONCAT($it_name, ",", $it_x, ",", $it_y)
    TFWRITE($items_filename, $new_item)
END_SUB

SUB(is_end_mainloop)
    find_pic_nw(1518,32,1562,73, "pause.bmp", 100, 7)
    IF($is_found == #FALSE)
        $mainloop = #FALSE
    END_IF
END_SUB

SUB(check_pause_zone)
    // Проверка зоны паузы
    IF(($setX > 1510) & ($setY < 85))
        $set = #TRUE
    END_IF
END_SUB

SUB(check_help_zone)
    // Проверка зоны помощи
    IF(($setX > 1470) & ($setY > 760))
        $set = #TRUE
    END_IF
END_SUB

SUB(waiting_for_items_to_click)
    // ожидание клика по предмету
    $wait_time = $_time_t - 1
    $set = #TRUE
    WHILE($set == #TRUE)
        IF($wait_time < $_time_t)
            //HINTPOPUP("Внимание! Готов принять команду")
            LOGWRITEC("Внимание! Готов принять команду", 8355839)
            $wait_time = $_time_t + 10
            WAITMS(50)
        END_IF
        
        IF(ISKEYDOWN(1) == #TRUE)
            $setX = $_xmouse - $wx1
            $setY = $_ymouse - $wy1
            $set = #FALSE
            WHILE(ISKEYDOWN(1) == #TRUE)  // ожидаем отпускания LM
                WAITMS(50)
            END_CYC
            
            check_pause_zone()
            check_help_zone()
        END_IF
        
        IF(ISKEYDOWN(#ESC) == #TRUE)
            $set = #FALSE
            LOGWRITEC("Работа прервана", 255)
            HINTPOPUP("Работа прервана")
            HALT
        END_IF
        WAITMS(30)
    END_CYC
    WAITMS(2500)
END_SUB

SUB(manual_click_words)
    // ручной поиск неизвесных картинок
    LOGWRITEC("Ручной поиск. Внимание!", 8355839)
    photo_items()
    create_ocr_par()
    ocr($ocrpar)
    read_ocr_result()
    
    UNDEFINE($need_words)
    FOR($res_i=0, $res_i < ARRSIZE($ocr_result))
        ARRPUSH($need_words, $ocr_result[$res_i])
    END_CYC
    
    $manual_select_word = #TRUE
    WHILE($manual_select_word == #TRUE)
        waiting_for_items_to_click()
        photo_items()
        create_ocr_par()
        ocr($ocrpar)
        read_ocr_result()
        FOR($res_i = 0, $res_i < ARRSIZE($need_words))
            // print("$res_i = ", $res_i)
            // print("$need_words[$res_i] = ", $need_words[$res_i])
            // print("$ocr_result[$res_i] = ", $ocr_result[$res_i])
            IF($need_words[$res_i] != $ocr_result[$res_i])
                $manual_select_word = #FALSE
                add_item($need_words[$res_i], $setX, $setY)
                $res_i = 100
            END_IF
        END_CYC
    END_CYC
END_SUB

SUB(passing_hidden_object_scene, $phos_scene_number)
    $scene_number = $phos_scene_number
    touch_for_start()
    get_hash_scene()
    read_data_scene()
    main_screenshot_scene()

    read_items()
    IF($loglevel >= 5)
        LOGWRITE("Массив $items:")
        LOG_ARR("$items", ARRSIZE($items))
    END_IF

    $mainloop = #TRUE
    WHILE($mainloop == #TRUE)
        $found_words = #TRUE  // ставим для принудительного первого пуска цикла
        WHILE($found_words == #TRUE)
            automat_click_words()
            IF($test > 1)  // после тестов убрать
                LOGWRITE("$found_words = ", $found_words)
                $found_words = #FALSE
            END_IF
        END_CYC
        
        is_end_mainloop()
        IF($test > 1)  // после тестов убрать
            LOGWRITE("$mainloop = ", $mainloop)
            // $mainloop = #FALSE
        END_IF
        // SCREENSHOTFIX($wx1,$wy1, $wx2,$wy2, "CM_1.bmp", 0)
        
        // ручная часть
        IF($mainloop == #TRUE)
            manual_click_words()
            is_end_mainloop()
        END_IF
    END_CYC
    LOGWRITE("Сцена ", $scene_number, " пройдена.", \n, "*************************************************")
END_SUB

//==============================================================================
//******************************************************************************

init()

$input_scene_number = INPUTBOX("Номер уровня", $default_scene_number, 2)
IF($input_scene_number == "")
    LOGWRITEC("Отменён выбор уровня. Стоп.", 255)
    HALT
END_IF
LOGWRITE("уровень: ", $input_scene_number)

get_location()
IF($location == "main")
    click($play_x, $play_y)
    WAITMS(500)
END_IF

passing_hidden_object_scene($input_scene_number)

HALT
